# vim:et sts=2 sw=2 ft=zsh
#
# A Zim version of Agnoster, a Powerline-inspired theme for Zsh.
#
# This theme requires a [patched Powerline
# font](https://github.com/powerline/fonts). In addition, it looks better with
# the [Solarized theme](https://github.com/altercation/solarized/).
#
# Based on the [original theme](https://github.com/agnoster/agnoster-zsh-theme),
# on the [Oh My Zsh version of
# Agnoster](https://github.com/robbyrussell/oh-my-zsh/blob/master/themes/agnoster.zsh-theme),
# and on the [Powerlevel9k theme](https://github.com/bhilburn/powerlevel9k).
#
# Uses the 'git-info' Zim module.
#

prompt_agnoster_main() {
  # This runs in a subshell
  RETVAL=${?}
  COLOR_BG=''

  prompt_agnoster_segment() {
    print -n "%K{${1}}"
    [[ -n ${COLOR_BG} ]] && print -n "%F{${COLOR_BG}}"
    print -n "${2}"
    COLOR_BG=${1}
  }

  prompt_agnoster_standout_segment() {
    print -n "%S%F{${1}}"
    [[ -n ${COLOR_BG} ]] && print -n "%K{${COLOR_BG}}%k"
    print -n "${2}%s"
    COLOR_BG=${1}
  }

  prompt_agnoster_end() {
    print -n "%k%F{${COLOR_BG}}%f "
  }

  prompt_agnoster_status() {
    local segment=''
    (( RETVAL )) && segment+=" %F{red}${RETVAL}"
    (( EUID == 0 )) && segment+=' %F{yellow}⚡'
    (( $(jobs -l | wc -l) )) && segment+=' %F{cyan}⚙'
    if [[ ${USER} != ${DEFAULT_USER} || -n ${SSH_CLIENT} ]]; then
       segment+=" %F{%(!.yellow.default)}${USER}@%m"
    fi
    if [[ -n ${segment} ]]; then
      prompt_agnoster_segment black "${segment} "
    fi
  }

  prompt_agnoster_pwd() {
    prompt_agnoster_standout_segment blue ' %~ '
  }

  prompt_agnoster_git() {
    if [[ -n ${git_info} ]]; then
      prompt_agnoster_standout_segment ${git_info[color]} " ${(e)git_info[prompt]} "
    fi
  }

  prompt_agnoster_status
  prompt_agnoster_pwd
  prompt_agnoster_git
  prompt_agnoster_end
}

prompt_agnoster_precmd() {
  (( ${+functions[git-info]} )) && git-info
}

prompt_agnoster_setup() {
  autoload -Uz add-zsh-hook && add-zsh-hook precmd prompt_agnoster_precmd

  prompt_opts=(cr percent sp subst)

  zstyle ':zim:git-info:branch' format ' %b'
  zstyle ':zim:git-info:commit' format '➦ %c'
  zstyle ':zim:git-info:ahead' format ' ↑%A'
  zstyle ':zim:git-info:behind' format ' ↓%B'
  zstyle ':zim:git-info:stashed' format ' ⍟%S'
  zstyle ':zim:git-info:indexed' format ' ✚'
  zstyle ':zim:git-info:unindexed' format ' ●'
  zstyle ':zim:git-info:action' format '  %s'
  zstyle ':zim:git-info:clean' format 'green'
  zstyle ':zim:git-info:dirty' format 'yellow'
  zstyle ':zim:git-info:keys' format \
      'prompt' '%b%c%A%B%S%i%I%s' \
      'color' '%C%D'

  PS1='$(prompt_agnoster_main)'
  RPS1=''
}

prompt_agnoster_setup "${@}"
